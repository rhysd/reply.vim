Describe :Repl
    Before each
        new!
    End

    After each
        bdelete!
        for b in term_list()
            execute 'bdelete!' b
        endfor
    End

    It does not open terminal when any REPL cannot be located
        Repl
        Assert LastMessage('reply.vim: No filetype is set for buffer')
        Assert Empty(term_list())
    End

    It opens REPL terminal for 'javascript' filetype
        setf javascript
        Repl
        Assert Equals(mode(), 't')
        Assert True(WithTimeout(1, {-> getline('.') !=# ''}))
        Assert Equals(getline('.'), '> ')
        Assert Equals(bufname('%'), 'reply: node')
        call term_sendkeys(bufnr('%'), "1 + 1\<CR>")
        Assert True(WithTimeout(1, {-> line('$') !=# 1}))
        Assert Equals(getline(1, '$'), ['> 1 + 1', '2', '> '])
    End

    It opens REPL terminal for 'node'
        Repl node
        Assert Equals(mode(), 't')
        Assert True(WithTimeout(1, {-> getline('.') !=# ''}))
        Assert Equals(getline('.'), '> ')
        Assert Equals(bufname('%'), 'reply: node')
        call term_sendkeys(bufnr('%'), "1 + 1\<CR>")
        Assert True(WithTimeout(1, {-> line('$') !=# 1}))
        Assert Equals(getline(1, '$'), ['> 1 + 1', '2', '> '])
    End

    It accepts command line options as rest of arguments
        Repl node -r path
        Assert Equals(mode(), 't')
        Assert True(WithTimeout(1, {-> getline('.') !=# ''}))
        Assert Equals(getline('.'), '> ')
        Assert Equals(bufname('%'), 'reply: node')
        call term_sendkeys(bufnr('%'), "path.join('foo', 'bar')\<CR>")
        Assert True(WithTimeout(1, {-> line('$') !=# 1}))
        Assert Match(getline(line('$')-1), "^'foo[\\/]bar'$")
    End

    It expands '%' in command line options as its full file path
        let file = AssetPath('repl-cmdopts-test.js')
        execute 'edit!' file
        Repl node -r %
        Assert Equals(mode(), 't')
        Assert True(WithTimeout(1, {-> getline('.') !=# ''}))
        Assert Equals(getline('.'), '> ')
        Assert Equals(bufname('%'), 'reply: node')
        call term_sendkeys(bufnr('%'), "global.foo()\<CR>")
        Assert True(WithTimeout(1, {-> line('$') !=# 1}))
        Assert Equals(getline(1, '$'), ['> global.foo()', "'foo'", '> '])
    End


    It sends selected strings in visual mode
        call setline('.', ['1 + 1'])
        let b = bufnr('%')
        normal! ggVGygv
        '<,'>Repl node

        " Cursor did not move
        Assert Equals(b, bufnr('%'))
        Assert Equals(mode(), 'n')

        let w = bufwinnr(term_list()[0])
        Assert NotEquals(w, -1)
        execute w . 'wincmd w'

        Assert True(WithTimeout(1, {-> line('$') > 2}))
        Assert Equals(getline(1, '$')[-3:-2], ['> 1 + 1', '2'])
    End

    It shows an error when a specified REPL is not defined
        Repl not-existing-repl-name
        Assert LastMessage("reply.vim: REPL 'not-existing-repl-name' is not defined or not installed")
    End

    " TODO: Test :Repl! to open multiple terminals from the same buffer
    " TODO: Test :Repl to open each terminal for each buffer
    " TODO: Test :Repl to regard g:reply_repls
End
